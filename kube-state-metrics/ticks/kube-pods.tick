// destination for the normalized data
var db = 'prometheus'
var rp = 'autogen'

var prefix = 'kube_pod'
var measurement = prefix + '_'
var destination = prefix + 's'

var not_measurement = 'kube_pod_container_'

// This should be equal to / shorter than the scraper period (eg 10s), to avoid including a window with multiple time occurrences of the same point.
// (I've seen occasional errors when attempt "equal to", so we're using "shorter than" for now.)
var cycle_time = 5s

// Normalize metric into measurement
stream
    |from()
    |where(lambda: strHasPrefix("__name__", measurement) AND !strHasPrefix("__name__", not_measurement))
    |window()
        .period(cycle_time)
        .every(cycle_time)
    |groupBy('namespace', 'pod')
    @groupConsolidateTrueFalseUnknown()
        .measurement('kube_pod_status_ready')
        .tag('condition')
        .trueValue(1)
        .falseValue(0)
        .unknownValue(-1)
    @groupConsolidateTrueFalseUnknown()
        .measurement('kube_pod_status_scheduled')
        .tag('condition')
        .trueValue(1)
        .falseValue(0)
        .unknownValue(-1)
    @groupJoinToSinglePoint()
        .stripPrefix(measurement)
        .dropTag('__name__')
        .ignoreValueFrom('kube_pod_info')
        .ignoreValueFrom('kube_pod_labels')
    |kapacitorLoopback()
        .database(db)
        .retentionPolicy(rp)
        .measurement(destination)

